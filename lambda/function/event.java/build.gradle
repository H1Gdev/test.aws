plugins {
    id 'java'
    id 'jacoco'
    id 'checkstyle'
    id 'io.freefair.aspectj.post-compile-weaving' version '8.2.2'
}

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jacoco {
    // https://www.jacoco.org/jacoco/trunk/doc/changes.html
    toolVersion = '0.8.10'
}

checkstyle {
    toolVersion = '10.12.3'
}

test {
    // Before 1.16.1, AWS_ACCESS_KEY_ID and AWS_SECRET_ACCESS_KEY are also required.
    environment 'AWS_REGION', 'ap-northeast-1'

    // https://docs.aws.amazon.com/xray/latest/devguide/xray-sdk-java-configuration.html#xray-sdk-java-configuration-envvars
    environment 'AWS_XRAY_CONTEXT_MISSING', 'IGNORE_ERROR'

    useJUnitPlatform()
    finalizedBy jacocoTestReport
}
jacocoTestReport {
    dependsOn test
}

dependencies {
    // https://docs.aws.amazon.com/lambda/latest/dg/java-package.html#java-package-libraries
    implementation 'com.amazonaws:aws-lambda-java-core:1.2.3'
    implementation 'com.amazonaws:aws-lambda-java-events:3.15.0'
    // https://docs.aws.amazon.com/sdk-for-java/latest/developer-guide/setup-project-gradle.html
    implementation platform('software.amazon.awssdk:bom:2.31.6')
    implementation 'software.amazon.awssdk:apache-client'
    implementation 'software.amazon.awssdk:sts'

    aspect 'software.amazon.lambda:powertools-logging-log4j:2.0.0'
    aspect 'software.amazon.lambda:powertools-parameters-ssm:2.0.0'
    aspect 'software.amazon.lambda:powertools-tracing:2.0.0'
    // https://docs.powertools.aws.dev/lambda/java/#java-compatibility
    implementation 'org.aspectj:aspectjrt:1.9.24'

    implementation 'org.apache.commons:commons-lang3:3.17.0'

    testImplementation platform('org.junit:junit-bom:5.12.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

task packageAllZip(type: Zip) {
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtimeClasspath
    }
}
task packageZip(type: Zip) {
    from compileJava
    from processResources
}
build.dependsOn packageZip
