plugins {
    id 'java'
    id 'jacoco'
    id 'checkstyle'
    id 'io.freefair.aspectj.post-compile-weaving' version '8.1.0'
}

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

jacoco {
    // https://www.jacoco.org/jacoco/trunk/doc/changes.html
    toolVersion = '0.8.10'
}

checkstyle {
    toolVersion = '10.12.3'
}

test {
    // Before 1.16.1, AWS_ACCESS_KEY_ID and AWS_SECRET_ACCESS_KEY are also required.
    environment 'AWS_REGION', 'ap-northeast-1'

    // https://docs.aws.amazon.com/xray/latest/devguide/xray-sdk-java-configuration.html#xray-sdk-java-configuration-envvars
    environment 'AWS_XRAY_CONTEXT_MISSING', 'IGNORE_ERROR'

    useJUnitPlatform()
    finalizedBy jacocoTestReport
}
jacocoTestReport {
    dependsOn test
}

dependencies {
    // https://docs.aws.amazon.com/lambda/latest/dg/java-package.html#java-package-libraries
    implementation 'com.amazonaws:aws-lambda-java-core:1.2.1'
    implementation 'com.amazonaws:aws-lambda-java-events:3.11.0'
    implementation 'software.amazon.awssdk:apache-client:2.20.25'
    implementation 'software.amazon.awssdk:sts:2.20.25'

    aspect 'software.amazon.lambda:powertools-logging:1.16.1'
    aspect 'software.amazon.lambda:powertools-parameters:1.16.1'
    aspect 'software.amazon.lambda:powertools-tracing:1.16.1'

    implementation 'org.apache.commons:commons-lang3:3.12.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
}

task packageAllZip(type: Zip) {
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtimeClasspath
    }
}
task packageZip(type: Zip) {
    from compileJava
    from processResources
}
build.dependsOn packageZip
